buildscript {
    repositories {
        mavenCentral()
        maven {
            name = "forge"
            url = "https://maven.minecraftforge.net/"
        }
        maven {
            url = "https://jitpack.io"
        }
    }
    dependencies {
        classpath('com.github.GTNewHorizons:ForgeGradle:1.2.11') {
            exclude group: "org.eclipse.equinox"
        }
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:1.2.71"
    }
}

apply plugin: 'java-library'
apply plugin: 'kotlin'

if(project.enable_lombok.toBoolean()) {
    dependencies {
        compileOnly 'org.projectlombok:lombok:1.18.4'
    }
}

repositories {
    mavenCentral()
}
apply from: "buildscript/forge-1.7.gradle"
apply plugin: 'maven-publish'


dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib:1.2.71"
}


def buildNumber = System.getenv('GITHUB_RUN_NUMBER') ?: 'local'
def modVersion = "1.0.${buildNumber}"
version = modVersion

def jarBaseName = "SideMCShop-${project.minecraft_version}"

task printVersion {
    doLast {
        println version
    }
}

task printTag {
    doLast {
        println "${modVersion}-mc${project.minecraft_version}"
    }
}

tasks.withType(Jar) {
    exclude("META-INF/*.SF", "META-INF/*.DSA", "META-INF/*.RSA")
}


publishing {
    publications {
        versioned(MavenPublication) {
            groupId = 'gg.essential.universal'
            artifactId = "Universal-1.7.10"
            version = modVersion

            artifact("$buildDir/libs/Universal-${modVersion}.jar")
        }
    }
    repositories {
        maven {
            name = "nexus"
            url = uri("https://nexus-smc.mtechlab.dev/repository/maven-releases/")
            credentials {
                username = project.findProperty("nexusUser") ?: System.getenv("NEXUS_USERNAME")
                password = project.findProperty("nexusPass") ?: System.getenv("NEXUS_PASSWORD")
            }
        }
    }
}